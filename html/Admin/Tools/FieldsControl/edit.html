<& /Admin/Elements/Header, Title => $title &>
<& /Elements/Tabs &>

<& /Elements/ListActions, actions => \@results &>

<style>
    .el span.label {
        display: block;
        text-align: left;
    }
    .elgrp div.el {
        display: inline-block;
        margin: 10px 10px;
        vertical-align: top;
    }
    .field-container .field div.el{
        margin: 0;
    }
    .el > legend,
    .elgrp > legend {
        font-size: 1.4em;
        /*color: #777;*/
        display: block;
        border-bottom: 2px solid black;
        padding: 3px;
        margin-bottom: 5px;
    }
    .ctlgrp {
        margin-bottom: 10px;
    }
    .field-list fieldset {
        margin: 1px 0;
    }
</style>

<form action="/Admin/Tools/FieldsControl/edit.html?id=<% $id %>&Create=<% $Create %>" method="post">
    <& Elements/FCRule, filling => $filling, data => $rule, aggreg_types => \%aggreg_types &>

%   if ($Create == 0) {
    <& /Elements/Submit, 
        Name => 'submit', 
        Label => loc('Save Changes'), 
        Back => 1,
        BackName => 'remove', 
        BackLabel => loc('Remove restriction'),
        BackOnClick => "return confirm('" . loc("Do you want to remove restriction?") . "');"
    &>
%   } else {
    <& /Elements/Submit, Name => 'submit', Label => loc('Save Changes') &>
%   }
</form>

<%init>
use Data::Dumper qw{Dumper};

my $title = loc('FieldsControl restriction');
unless ($session{'CurrentUser'}->HasRight( Object=> $RT::System, Right => 'SuperUser')) {
    Abort(loc('This feature is only available to system administrators'));
}

my %config = RT::Extension::FieldsControl::load_config;
my $restrictions = $config{restrictions};
my @results;

$id += 0;
$Create += 0;

my $rule = $restrictions->{$id};
unless ($Create) {
    unless ($rule) {
        Abort(loc('ERROR: Invalid id'));
    }
}


my @ops = sort keys %${RT::Extension::FieldsControl::available_ops};
my @fields_list = sort keys %{RT::Extension::FieldsControl::get_fields_list()};
my %aggreg_types = %${RT::Extension::FieldsControl::aggreg_types};
my $filling = {
    fields => \@fields_list,
    operations => \@ops
};


if ($ARGS{'remove'}) {
    my $res = RT::Extension::FieldsControl::write_config(delete_r => {$id => $restrictions->{$id}});
    if ($res) {
        push @results, loc('Restriction #[_1] removed', $id);
    } else {
        push @results, loc('ERROR: Cannot remove restriction #[_1]. Contact your RT administrator');
    }
    MaybeRedirectForResults(
        Actions   => \@results,
        Path      => "/Admin/Tools/FieldsControl"
    );
}


if ($ARGS{'submit'}) {
    my $i = 1;
    my $tickets = RT::Tickets->new( RT::SystemUser ); #Object for checking SQL correctness
    $rule = {};

    # Retrieve rule name
    push @results, loc("ERROR: Name field is required") unless ($ARGS{'name'});
    $rule->{'rulename'} = $ARGS{'name'};

    # Rule enabled
    $rule->{'enabled'} = (exists($ARGS{'enabled'}));
    
    # Error message comment
    $rule->{comment} = $ARGS{comment};

    # Apply restriction on pages
    my @apply_pages = ();
    foreach my $page (@RT::Extension::FieldsControl::spy_pages) {
        my $k = 'on-' . lc $page;
        push @apply_pages, $page if (exists $ARGS{$k} && defined $ARGS{$k});
    }
    $rule->{apply_pages} = \@apply_pages;

    # Check whether TicketSQL is correct
    my $ssql = $ARGS{searchsql};
    if ($ssql) {
        my ($res, $msg) = $tickets->FromSQL($ssql);
        unless ($res) {
            push @results, loc("ERROR: Bad TicketSQL: [_1]", $msg);
        }
    }
    $rule->{searchsql} = $ssql;
    
    # Retrieve fields
    my %field_types = (
        'sfields' => 'Applies to', #loc
        'rfields' => 'Fails if', #loc
    );
    foreach my $field_type (keys %field_types) {
        my $j = 1;
        $rule->{$field_type} = [];

        while (defined $ARGS{join('-', ($field_type, $j, 'name'))})
        {
            my $f = $ARGS{join('-', ($field_type, $j, 'name'))};
            my $op = $ARGS{join('-', ($field_type, $j, 'op'))};
            my $val = $ARGS{join('-', ($field_type, $j, 'value'))};

            if (defined $ARGS{join('-', ($field_type, $j, 'delete'))}) {
                $j++;
                next;
            }

            push @{$rule->{$field_type}}, {
                'field' => $f,
                'op' => $op,
                'value' => $val
            };

            unless ((grep { $_ eq $f } @fields_list)
                && (grep { $_ eq $op } @ops))
            {
                push @results, loc('ERROR: Section "[_1]", test #[_2]', loc($field_types{$field_type}), $j);
            }
            $j++;
        }

        my $aggreg_type = $ARGS{join('-', ($field_type, 'aggreg'))};
        if (grep {$aggreg_type eq $_} keys %aggreg_types) 
        {
            $rule->{$field_type . 'aggreg'} = $aggreg_type;
        } else {
            push @results, loc('ERROR: bad aggregation in section "[_1]"', loc($field_types{$field_type}));
        }
    }
    unless (@{$rule->{'rfields'}}) {
        push @results, loc('ERROR: "[_1]" section must contain at least one test', loc($field_types{'rfields'}));
    }

    unless (@results) {
        my ($res, $msg);
        if ($Create) {
            ($res, $msg) = RT::Extension::FieldsControl::write_config(create_r => [$rule]);
        } else {
            ($res, $msg) = RT::Extension::FieldsControl::write_config(update_r => {$id => $rule});
        }

        if ($res) {
            push @results, loc('Configuration saved');
        } else {
            push @results, loc('ERROR: Cannot save configuration: [_1]', $msg);
        }

        MaybeRedirectForResults(
            Actions   => \@results,
            Path      => "/Admin/Tools/FieldsControl"
        );
    }

}
</%init>

<%args>
$id => 0
$Create => 0
$Remove => 0
</%args>