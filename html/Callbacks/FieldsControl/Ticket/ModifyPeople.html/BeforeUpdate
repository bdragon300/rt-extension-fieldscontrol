<%args>
$ARGSRef
$TicketObj
$skip_update
$results
</%args>

<%init>
# Check whether ticket changed by someone since last page load
if ($ARGSRef->{FCAbort}) {
    $$skip_update = 1;
    push @$results, 
        loc('ERROR: The ticket was changed by someone at the same time as you. Please make your changes again.');

} elsif ($ARGSRef->{SubmitTicket}) {
    my $lastupdated = $ARGSRef->{'FCLastUpdated-Ticket-cur'} || '0';
    my $check = RT::Extension::FieldsControl::check_ticket($TicketObj, $ARGSRef, 'ModifyPeople');

    if ($TicketObj->LastUpdated gt $lastupdated
        && $check->{tests_refer_to_ticket})
    {
        my @u = (
            RT->Config->Get('WebURL') =~ s/\/+$//r, # remove trailing slash /
            '/Ticket/ModifyPeople.html?id=',
            $TicketObj->id,
            '&FCAbort=1'
        );
        $m->redirect(join('', @u));
        
    } elsif (my @e = @{$check->{errors}}) {
        $$skip_update = 1;
        push @$results, loc('ERROR: request aborted because of following restrictions:');
        while (my $r = shift @e) {
            push @$results, 
                ".. " . $r->{rulename},
                loc("...... Failed fields: ~[ [_1] ~]", join(', ', map { loc($_) } @{shift @e}));

            if ($r->{comment}) {
                push @$results, loc("...... Comment: [_1]", $r->{comment});
            }
        }
    }

    # There is no redirect after successfull change with results=..., therefore
    # FCLastUpdate holds old value. Delete key in order to reload actual LastUpdated
    delete $ARGSRef->{'FCLastUpdated-Ticket-cur'};
}
</%init>
